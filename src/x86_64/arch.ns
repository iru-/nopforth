\ SPDX-License-Identifier: MIT
\ Copyright (c) 2018-2020 IruatÃ£ Martins dos Santos Souza

macro
: 4! ( n a -> )
   $004D8B48 4,           \ mov (%rbp), %rcx
   $0889 2,               \ mov %ecx, (%rax)
   $086D8D4800458B48 ,    \ drop
   $086D8D4800458B48 , ;  \ drop

: r@
   $00458948F86D8D48 ,  \ dup
   $24048B48 4, ;       \ mov (%rsp), %rax

: rdrop   $48 1, $0824648D 4, ;  \ lea 8(%rsp), %rsp
: rdec    $240CFF48 4, ;   \ decq (%rsp)

: rshift ( n places -> n' )
   $C18948 3,           \ mov %rax, %rcx
   $086D8D4800458B48 ,  \ drop
   $E8D348 3, ;         \ shr %cl, %rax

: lshift ( n places -> n' )
   $C18948 3,           \ mov %rax, %rcx
   $086D8D4800458B48 ,  \ drop
   $E0D348 3, ;         \ shl %cl, %rax

: if ( -> a )
   $C08548 3,           \ test %rax, %rax
   $086D8D4800458B48 ,  \ drop
   $840F 2,             \ jz...
   here
   0 4, ;               \ ...0

: then ( a -> )   0 hole !  here  over 4 + -  swap 4! ;

: branch ( dst -> )   here 5 +  -  $E9 1, 4, ;
: branch-unresolved ( -> a )   $E9 1,  here  0 4, ;

: negate ( n -> n' )   $D8F748 3, ;  \ neg %rax

forth
: call-rel! ( 'code dst -> #used )
   $E8 over 1!
   1 + dup push   \ save address where to store the offset
   4 +   -        \ compute offset
   pop 4!  5 ;

\ compile an absolute call:
\ movabs dst, %r11
\ call *%r11
: dst>r11! ( dst a -> a+10 )     a!  $BB $49  1!+ 1!+ !+  a ;
: callr11! ( a -> )              a!  $D3 $FF $41  1!+ 1!+ 1!+ ;
: call-abs! ( dst a -> #used )   dst>r11! callr11!  13 ;

: within ( u lo hi -> flag )  push over <=  swap pop <  and ;

: abs>off ( 'code dst -> )   5 + - ;
: 32bits? ( off -> flag )    -2147483648 2147483648 within ;

: call! ( 'code dst -> #consumed )
   here hole !  over over abs>off 32bits? if call-rel! exit then call-abs! ;

