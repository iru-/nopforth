\ SPDX-License-Identifier: MIT
\ Copyright (c) 2018-2020 IruatÃ£ Martins dos Santos Souza

forth
32 value bl

: emit ( b -> )   here 1!  here 1 type ;

: cr      10 emit ;
: space   bl emit ;
: spaces ( u -> )   for space next ;

: printable? ( c -> flag )    bl 127 within ;

: char ( -> b )     bl word drop 1@ ;
macro
: [char] ( -> b )   char ` lit ;
forth

: s>z ( a u -> a' )     here push  dup allot  r@ swap move  0 1,  pop ;
: zlen ( a -> u )       a! 0 begin 1@+ while 1 + repeat ;
: z" ( -> a )           [char] " word s>z ;
: z>mem ( a -> a' u )   dup zlen ;

macro
: memlit ( a len -> )
   dup push  ` branch-unresolved  here push push
   \ r: len addr-of-string-start addr-of-branch-dst
   mem,  pop ` then  pop ` lit  pop ` lit ;

macro : "   [char] " word  ` memlit ;
forth : " ( -> a u )
  [char] " word  tuck  here dup push  over allot  swap move  pop swap ;

: println   type cr ;

macro : ."   ` "  [f'] type call, ;
forth : ."   [char] " word type ;


: depth ( -> u )   S0 sp@ - 8 /  2 - ;


